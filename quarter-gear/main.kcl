fn gearSketch = (radius, numTeeth, height, center, plane, totalDegrees, startDegree) => {
  arcWidth = totalDegrees / numTeeth
  cx = center[0]
  cy = center[1]

  fn pointFor = (theta, radius) => {
    return [
      cx + cos(toRadians(theta)) * radius,
      cy + sin(toRadians(theta)) * radius
    ]
  }

 theta = (totalDegrees/3) / numTeeth
  fn drawOneSectorOfGear = (i, acc) => {
    angleStart = startDegree + int(i - 1) * arcWidth
   
    intervals = [
      angleStart,
      angleStart + theta,
      angleStart + 2 * theta,
      angleStart + 3 * theta
    ]

    // Returns arc data.
    fn ofIntervals = (start, end, r) => {
      return {
        angleStart: intervals[start],
        angleEnd: intervals[end],
        radius: r
      }
    }
    sector = acc
    // Draw gear body, along the inner circle.
    |> arc(ofIntervals(0, 1, radius), %)
    // Side of tooth, from base up to the tip
    |> lineTo(pointFor(intervals[1], radius + height), %)
    // Outer edge of tooth
    |> arc(ofIntervals(1, 2, radius + height), %)
    // End of tooth, tip back down to the base
    |> lineTo(pointFor(intervals[2], radius), %)
    // Tooth is finished, draw more of the gear body.
    |> arc(ofIntervals(2, 3, radius), %)
    
    return sector
  }

  return startSketchOn(plane)
  |> startProfileAt(center, %)
  |> lineTo([
       cos(toRadians(startDegree)) * radius + cx,
       sin(toRadians(startDegree)) * radius + cy
     ], %)
  |> reduce([1 .. int(numTeeth)], %, drawOneSectorOfGear)
  |> lineTo(center, %)
  |> close(%)
}


fn helicalGear = (radius, numTeeth, height, center, pitch, numLofts) => {
  return [0..int(numLofts-1)]
  |> map(%, (numLoft) => {
        plane = offsetPlane("YZ", height * numLoft)
        return gearSketch(radius, numTeeth, height, center, plane, 90, pitch * numLoft)
    })
  |> loft(%, {vDegree: 1})
}

radius = 15
numTeeth = 10
height = 2
center = [40, 10]
plane = "XY"
totalDegrees = 90
startDegree = 0
pitch = 10
numLofts = 2

helicalGear(radius, numTeeth, height, center, pitch, numLofts)
